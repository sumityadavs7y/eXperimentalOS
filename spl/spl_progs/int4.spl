alias physicalSP S0;
physicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);
alias sysCallNo S1;
sysCallNo = [physicalSP - 1];
alias filedesc S2;
filedesc = [physicalSP - 4];
alias wordToWrite S3;
wordToWrite = [physicalSP - 3];
alias currentPID S4;
currentPID = (PTBR - 1024) / 8;
alias currentPCB S5;
currentPCB = READY_LIST + currentPID * 32;

if(sysCallNo == 5) then									//WRITE
	if(filedesc < 0 || filedesc > 7) then				//check for invalid discriptor
		[physicalSP - 2] = -1;
		ireturn;
	endif;
		
	alias pPFileTableIndex S6;
	pPFileTableIndex = filedesc * 2;					//address of PPOFT w.r.t. PPOFT
	alias fileTableIndex S7;
	fileTableIndex = currentPCB + pPFileTableIndex + 15;	//address of PPOFT entry
	
	if([fileTableIndex] == -1) then							//if file not present
		[physicalSP - 2] = -1;
		ireturn;
	endif;
	
	alias lseek S8;
	lseek = [fileTableIndex + 1];							//get lseek index
	fileTableIndex = [fileTableIndex];						//get file SWOFT index from PPOFT

	alias fatEntry S9;
	fatEntry = [FILE_TABLE + (fileTableIndex * 2)];			//get index of FAT entry from SWOFT
	
	alias basicBlock S10;
	basicBlock = [FAT + (fatEntry * 8) + 2];				//get basic block no from FAT
	
	load(1, basicBlock);									//load basic block to scratchpad
	
	alias pageToWrite S11;
	pageToWrite = [512 + (lseek / 512)];					//get pageno to write?

	if(pageToWrite == -1) then								//page is not entered in basic block
		alias df S12;
		df = 3072;
		while(df < 3520) do									//search for empty block in dfl
			if([df] == 0) then
				break;
			endif;
			df = df + 1;
		endwhile;
		if(df >= 3520) then									//if no block found
			[physicalSP - 2] = -1;
			ireturn;
		endif;
		[512 + (lseek / 512)] = df - 3072;					//if found make entry in basic block
		[df] = 1;											//entry in dfl
		alias fatValue S13;
		fatValue = FAT + (fatEntry * 8);					//FAT entry address
		[fatValue + 1] = [fatValue + 1] + 512;				//increase 512 of file size
		store(1, basicBlock);								//store basic block
		store(5, 19);										//store FAT
		store(6, 20);										//store dfl
		pageToWrite = df - 3072;							//get pageno to write
	endif;

	load(1, pageToWrite);									//load page to scratchpad
	[512 + (lseek % 512)] = wordToWrite;					//write words
	store(1, pageToWrite);									//store changes to page
	[currentPCB + pPFileTableIndex + 16] = [currentPCB + pPFileTableIndex + 16] + 1; //increase lseek position
	[physicalSP - 2] = 0;									//return success
	ireturn;
endif;
